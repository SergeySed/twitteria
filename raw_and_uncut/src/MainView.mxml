<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" creationComplete="init()">
	<mx:Script>
	<![CDATA[
		import mx.collections.ArrayCollection;
		import twitter.api.events.TwitterEvent;
		import twitter.api.data.TwitterUser;
		import twitter.api.Twitter;
		
		public var username:String;
		public var password:String;
		public var twitterService:Twitter;
		
		[Bindable]
		private var charactersLeft:int = 140;
		
		[Bindable]
		public var currentTweets:ArrayCollection; // <TwitterStatus>
		
		private function init():void {
			twitterService = new Twitter();
			twitterService.setAuthenticationCredentials(username, password);
			twitterService.addEventListener(TwitterEvent.ON_FRIENDS_TIMELINE_RESULT, friendsTimlineLoaded);
			twitterService.addEventListener(TwitterEvent.ON_SET_STATUS, statusSet);
			reload();
		}
		
		private function reload():void {
			twitterService.loadFriendsTimeline(username);
		}
		
		private function setStatus():void {
			twitterService.setStatus(statusText.text);
		}
		
		private function friendsTimlineLoaded(te:TwitterEvent):void {
			currentTweets = new ArrayCollection(te.data as Array);
		}
		
		private function statusSet(te:TwitterEvent):void {
			statusText.text = '';
			countCharactersLeft();
			reload();
		}
		
		private function countCharactersLeft():void {
			var chars:int = 140 - statusText.text.length;
			charactersLeft = Math.max(chars, 0);
			if (charactersLeft > 10) {
				charactersLeftLabel.setStyle("color", 0xEEEEEE);
			} else {
				charactersLeftLabel.setStyle("color", 0xFF5555);
			}
		}
		
	]]>
	</mx:Script>
	
	<mx:List id="statusList" width="100%" height="100%" styleName="statusList" itemRenderer="TwitterStatusRenderer" dataProvider="{currentTweets}" />
	<mx:HBox width="100%" styleName="instructions">
		<mx:Label text="What are you doing?" />
		<mx:Spacer width="100%" />
		<mx:Label id="charactersLeftLabel" text="{charactersLeft}" styleName="charactersLeft" />
	</mx:HBox>
	<mx:TextInput id="statusText" enter="setStatus()" width="100%" maxChars="140" keyUp="countCharactersLeft()" />
	
</mx:VBox>
